apply plugin: 'java'
apply plugin: 'maven-publish'

group = 'net.minecraftforge.gradle'
version = '3.0.0-SNAPSHOT'

sourceSets {
    common
    mcp
    patcher
}

configurations {
    mcpImplementation.extendsFrom commonImplementation
    patcherImplementation.extendsFrom commonImplementation
    compile.extendsFrom mcpImplementation, patcherImplementation
}

jar {
    from sourceSets.common.output
    from sourceSets.mcp.output
    from sourceSets.patcher.output
}

repositories {
    jcenter()
    maven { url = 'http://files.minecraftforge.net/maven' }
}

dependencies {
    commonImplementation gradleApi()
    commonImplementation 'com.cloudbees:diff4j:1.2'
    commonImplementation 'commons-io:commons-io:2.4'
    commonImplementation 'com.google.code.gson:gson:2.8.5'
    commonImplementation 'com.google.guava:guava:26.0-jre'
    commonImplementation 'de.siegmar:fastcsv:1.0.2'
    commonImplementation 'net.minecraftforge:Srg2Source:4.2.+'
    commonImplementation 'com.amadornes:artifactural:1.0.+'


    mcpImplementation sourceSets.common.output
    patcherImplementation sourceSets.mcp.output
    patcherImplementation sourceSets.common.output
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "$buildDir/repo"
        }
    }
}
