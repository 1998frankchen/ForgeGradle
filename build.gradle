apply plugin: 'java'
apply plugin: 'maven-publish'

group = 'net.minecraftforge.gradle'
version = '3.0.0-SNAPSHOT'

sourceSets {
    common
    mcp
    patcher
}

configurations {
    mcpImplementation.extendsFrom commonImplementation
    patcherImplementation.extendsFrom commonImplementation
    compile.extendsFrom mcpImplementation, patcherImplementation
}

jar {
    from sourceSets.common.output
    from sourceSets.mcp.output
    from sourceSets.patcher.output
}

repositories {
    jcenter()
}

dependencies {
    commonImplementation gradleApi()
    commonImplementation 'com.cloudbees:diff4j:1.2'
    commonImplementation 'com.google.code.gson:gson:2.8.5'
    commonImplementation 'com.google.guava:guava:26.0-jre'


    mcpImplementation sourceSets.common.output
    patcherImplementation sourceSets.mcp.output
    patcherImplementation sourceSets.common.output
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "$buildDir/repo"
        }
    }
}
